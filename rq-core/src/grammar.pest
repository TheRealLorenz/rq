file = { SOI ~ (request ~ (DELIM ~ request)*)? ~ EOI}

DELIM = _{ "###" }
WHITESPACE = _{ " " }

char = { !(" " | "\n" | "\t") ~ ANY }

request = {
	NEWLINE* ~
	method? ~ url ~ query? ~ version? ~ NEWLINE ~
	headers? ~
	NEWLINE ~
	body? ~
	NEWLINE*
}

method = { "GET" | "DELETE" | "POST" | "PUT" }

url = @{ (!"?" ~ char)+ }

query = @{
	PUSH(NEWLINE ~ (" " | "\t")* | "") ~
	"?" ~ query_item ~
	(PEEK ~ "&" ~ query_item)* ~
	DROP
}
query_item = ${ query_name ~ "=" ~ query_value }
query_name = @{ (!"=" ~ char)+ }
query_value = @{
	(
		PUSH("\"" | "'") ~
		(!PEEK ~ ANY)+ ~
		POP
	) |
	(!"&" ~ char)+
}

version = { "HTTP/" ~ ("0.9" | "1.0" | "1.1" | "2.0" | "3.0") }

headers = { (header ~ NEWLINE)+ }
header = { header_name ~ ": " ~ header_value }
header_name = @{ (!":" ~ char)+ }
header_value = @{ (" " | char)+ }

body = { (!DELIM ~ ANY)+ }

