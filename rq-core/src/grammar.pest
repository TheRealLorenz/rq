file = { SOI ~ (request ~ (delimiter ~ request)*)? ~ EOI}

request = {	
	request_line ~
    headers? ~
    NEWLINE ~
    body?
}

request_line = _{ (method ~ " "+)? ~ uri ~ query? ~ (" "+ ~ version)? ~ NEWLINE }
uri = { (!(whitespace| "?") ~ ANY)+ }
method = { ("GET" | "DELETE" | "POST" | "PUT") }
version = { "HTTP/" ~ ("0.9" | "1.0" | "1.1" | "2.0" | "3.0") }
whitespace = _{ " " | "\t" | NEWLINE }

query = { "?" ~ query_item ~ ("&" ~ query_item)* }
query_item = { query_name ~ "=" ~ query_value }
query_name = { (!(NEWLINE | "=") ~ ANY)+ }
query_value = { (!(NEWLINE | "&") ~ ANY)+ }

headers = { header+ }
header = { header_name ~ ":" ~ whitespace ~ header_value ~ NEWLINE }
header_name = { (!(NEWLINE | ":") ~ ANY)+ }
header_value = { (!NEWLINE ~ ANY)+ }

body = { (!delimiter ~ ANY)+ }
delimiter = { "#"{3} ~ NEWLINE+ }
