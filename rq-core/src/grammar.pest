file = {
	SOI ~
	((request | var_def_block) ~
	(DELIM ~ (request | var_def_block))*)? ~
	EOI
}

DELIM = _{ "###" }
WHITESPACE = _{ " " }
VAR_BEGIN = _{ "{{" }
VAR_END = _{ "}}" }

char = { !(" " | "\n" | "\t") ~ ANY }

request = {
	NEWLINE* ~
	method? ~ url ~ query? ~ version? ~ NEWLINE ~
	headers? ~
	NEWLINE ~
	body? ~
	NEWLINE*
}

method = { "GET" | "DELETE" | "POST" | "PUT" }

url = ${ (var | url_fragment)+ }
url_fragment = @{ (!(VAR_BEGIN | "?") ~ char)+ }

query = @{
	PUSH(NEWLINE ~ " "* | "") ~
	"?" ~ query_item ~
	(PEEK ~ "&" ~ query_item)* ~
	DROP
}
query_item = ${ query_name ~ "=" ~ query_value }
query_name = @{ (!"=" ~ char)+ }
query_value = $ { (var | query_value_fragment)+ }
query_value_fragment = @{
	(
		PUSH("\"" | "'") ~
		(!PEEK ~ ANY)+ ~
		POP
	) |
	(!("&" | VAR_BEGIN) ~ char)+
}

version = { "HTTP/" ~ ("0.9" | "1.0" | "1.1" | "2.0" | "3.0") }

headers = { (header ~ NEWLINE)+ }
header = { header_name ~ ":" ~ header_value }
header_name = @{ (!":" ~ char)+ }
header_value = ${ (var | header_value_fragment)+ }
header_value_fragment = @{ (!VAR_BEGIN ~ (char | " "))+ }

body = ${ (var | body_fragment)+ }
body_fragment = @{ (!(VAR_BEGIN | DELIM)~ ANY)+ }

var = ${ VAR_BEGIN ~ var_name ~ VAR_END }
var_name = @{ (!VAR_END ~ char)+ }

var_def_block = { (var_def ~ NEWLINE*)+ }
var_def = ${ "@" ~ var_def_name ~ " "? ~ "=" ~ " "? ~ var_def_value }
var_def_name = @{ (!"=" ~ char)+ }
var_def_value = ${ (var | var_def_value_fragment)+ }
var_def_value_fragment = @{
	(
		PUSH("'" | "\"") ~
		(!PEEK ~ ANY)+ ~
		POP
	) |
	(!VAR_BEGIN ~ char)+
}
